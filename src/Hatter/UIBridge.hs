{-# LANGUAGE ForeignFunctionInterface #-}
-- | Low-level FFI bindings to the C UI bridge (@cbits/ui_bridge.c@).
--
-- These functions delegate to platform-native implementations on
-- Android/iOS and fall back to stderr stubs on desktop.
module Hatter.UIBridge
  ( NodeType(..)
  , nodeTypeToInt
  , PropId(..)
  , propIdToInt
  , EventType(..)
  , eventTypeToInt
  , createNode
  , setStrProp
  , setNumProp
  , setImageData
  , setHandler
  , addChild
  , removeChild
  , destroyNode
  , setRoot
  , clear
  )
where

import Data.ByteString (ByteString)
import Data.ByteString qualified as BS
import Data.Int (Int32)
import Data.Text (Text, unpack)
import Data.Word (Word8)
import Foreign.C.String (CString, withCString)
import Foreign.C.Types (CInt(..), CDouble(..))
import Foreign.Ptr (Ptr, castPtr)
import Unwitch.Convert.Int qualified as Int
import Unwitch.Convert.Int32 qualified as Int32
import Unwitch.Convert.CInt qualified as CInt

-- | Widget node types corresponding to @UI_NODE_*@ in @UIBridge.h@.
data NodeType
  = NodeText
  | NodeButton
  | NodeColumn
  | NodeRow
  | NodeTextInput
  | NodeScrollView
  | NodeImage
  | NodeMapView
  | NodeWebView
  | NodeStack
  | NodeHorizontalScrollView
  deriving (Show, Eq, Enum, Bounded)

-- | Map a 'NodeType' to its C integer code.
nodeTypeToInt :: NodeType -> Int32
nodeTypeToInt NodeText       = 0
nodeTypeToInt NodeButton     = 1
nodeTypeToInt NodeColumn     = 2
nodeTypeToInt NodeRow        = 3
nodeTypeToInt NodeTextInput  = 4
nodeTypeToInt NodeScrollView = 5
nodeTypeToInt NodeImage      = 6
nodeTypeToInt NodeMapView    = 7
nodeTypeToInt NodeWebView    = 8
nodeTypeToInt NodeStack      = 9
nodeTypeToInt NodeHorizontalScrollView = 10

-- | Property identifiers for 'setStrProp' and 'setNumProp'.
data PropId
  = PropText
  | PropColor
  | PropHint
  | PropBgColor
  | PropFontSize
  | PropPadding
  | PropInputType
  | PropGravity
  | PropImageResource
  | PropImageFile
  | PropScaleType
  | PropWebViewUrl
  | PropMapLat
  | PropMapLon
  | PropMapZoom
  | PropMapShowUserLoc
  | PropTranslateX
  | PropTranslateY
  | PropAutoFocus
  | PropTouchPassthrough
  deriving (Show, Eq, Enum, Bounded)

-- | Map a 'PropId' to its C integer code.
propIdToInt :: PropId -> Int32
propIdToInt PropText          = 0
propIdToInt PropColor         = 1
propIdToInt PropHint          = 2
propIdToInt PropBgColor       = 3
propIdToInt PropImageResource = 4
propIdToInt PropImageFile     = 5
propIdToInt PropFontSize      = 0
propIdToInt PropPadding       = 1
propIdToInt PropInputType     = 2
propIdToInt PropGravity       = 3
propIdToInt PropScaleType     = 4
propIdToInt PropWebViewUrl    = 6
propIdToInt PropMapLat        = 5
propIdToInt PropMapLon        = 6
propIdToInt PropMapZoom       = 7
propIdToInt PropMapShowUserLoc = 8
propIdToInt PropTranslateX    = 9
propIdToInt PropTranslateY    = 10
propIdToInt PropAutoFocus          = 11
propIdToInt PropTouchPassthrough  = 12

-- | Event types corresponding to @UI_EVENT_*@ in @UIBridge.h@.
data EventType
  = EventClick
  | EventTextChange
  deriving (Show, Eq, Enum, Bounded)

-- | Map an 'EventType' to its C integer code.
eventTypeToInt :: EventType -> Int32
eventTypeToInt EventClick      = 0
eventTypeToInt EventTextChange = 1

-- Raw FFI imports
foreign import ccall "ui_create_node"    c_createNode   :: CInt -> IO CInt
foreign import ccall "ui_set_str_prop"   c_setStrProp   :: CInt -> CInt -> CString -> IO ()
foreign import ccall "ui_set_num_prop"   c_setNumProp   :: CInt -> CInt -> CDouble -> IO ()
foreign import ccall "ui_set_image_data" c_setImageData :: CInt -> Ptr Word8 -> CInt -> IO ()
foreign import ccall "ui_set_handler"    c_setHandler   :: CInt -> CInt -> CInt -> IO ()
foreign import ccall "ui_add_child"      c_addChild     :: CInt -> CInt -> IO ()
foreign import ccall "ui_remove_child"   c_removeChild  :: CInt -> CInt -> IO ()
foreign import ccall "ui_destroy_node"   c_destroyNode  :: CInt -> IO ()
foreign import ccall "ui_set_root"       c_setRoot      :: CInt -> IO ()
foreign import ccall "ui_clear"          c_clear        :: IO ()

-- | Create a native node of the given type. Returns an opaque node ID.
createNode :: NodeType -> IO Int32
createNode nt = CInt.toInt32 <$> c_createNode (Int32.toCInt (nodeTypeToInt nt))

-- | Set a string property on a node.
setStrProp :: Int32 -> PropId -> Text -> IO ()
setStrProp nodeId propId value =
  withCString (unpack value) $ \cstr ->
    c_setStrProp (Int32.toCInt nodeId) (Int32.toCInt (propIdToInt propId)) cstr

-- | Set a numeric property on a node.
setNumProp :: Int32 -> PropId -> Double -> IO ()
setNumProp nodeId propId value =
  c_setNumProp (Int32.toCInt nodeId) (Int32.toCInt (propIdToInt propId)) (realToFrac value)

-- | Set raw image data (PNG/JPEG bytes) on a node.
setImageData :: Int32 -> ByteString -> IO ()
setImageData nodeId imageBytes =
  BS.useAsCStringLen imageBytes $ \(ptr, len) ->
    c_setImageData (Int32.toCInt nodeId) (castPtr ptr) (maybe 0 id (Int.toCInt len))

-- | Register an event handler on a node. The @callbackId@ is looked up
-- in the 'RenderState' callback registry when the event fires.
setHandler :: Int32 -> EventType -> Int32 -> IO ()
setHandler nodeId eventType callbackId =
  c_setHandler (Int32.toCInt nodeId) (Int32.toCInt (eventTypeToInt eventType)) (Int32.toCInt callbackId)

-- | Add a child node to a parent container.
addChild :: Int32 -> Int32 -> IO ()
addChild parentId childId =
  c_addChild (Int32.toCInt parentId) (Int32.toCInt childId)

-- | Remove a child node from a parent container.
removeChild :: Int32 -> Int32 -> IO ()
removeChild parentId childId =
  c_removeChild (Int32.toCInt parentId) (Int32.toCInt childId)

-- | Destroy a node and free its native resources.
destroyNode :: Int32 -> IO ()
destroyNode nodeId =
  c_destroyNode (Int32.toCInt nodeId)

-- | Set a node as the root of the display.
setRoot :: Int32 -> IO ()
setRoot nodeId =
  c_setRoot (Int32.toCInt nodeId)

-- | Clear all nodes (called before re-render).
clear :: IO ()
clear = c_clear
